10 rem caverns by r vonk (c) 2000
20 rem originally part of a series, now a standalone game
30 rem updated in 2021
40 poke 53280,8:poke 53281,8:poke 646,1
50 cv=1:h=100:sc=0:l=1:z=rnd(-ti)
60 print"“":print"you are in cavern";cv;"-";l
70 print:print"health:";h;"%"
80 print:print"your score is";sc:print:print:print
90 if rnd(0)>.7 then 210
100 t=int(rnd(0)*4+2): d1=int(rnd(0)*t+1)
110 print"you see";t;"tunnels, all of them dark and"
120 print"possibly dangerous. which one do you"
130 print"want to take";
140 input c:if c<1 or c>t then 140
150 if c<>d1 then 190
160 print:print"you fall down a steep decline and injure";
170 print"yourself.":h=h-int(rnd(0)*3+2):l=l+1:if h<=0 then 930
180 goto 210
190 print:print"you make your way through the tunnel"
200 print"safely.":l=l+1
210 if rnd(0)>.3 then 400
220 print:print"watch out!":print
230 m=int(rnd(0)*4+1)
240 if m=1 then m$="gremlin"
250 if m=2 then m$="mutant bat"
260 if m=3 then m$="serpent"
270 if m=4 then m$="ghoul"
280 print:print"a ";m$;" guards this tunnel."
290 print:print"do you wish to fight or retreat"
300 input"(f or r)";c$:if c$="f" then 340
310 if c$<>"r" then 300
320 if rnd(0)<.2 then print:print"the ";m$;" stops you.":goto 340
330 print:print"you manage to escape.":goto 400
340 if rnd(0)>.4 then 390
350 print:print"*ugh* you've been defeated, and the"
360 print m$;" has left you with a severe"
370 print"wound.":h=h-int(rnd(0)*5+l):if h<=0 then 930
380 goto 400
390 print:print"you have slain the ";m$;".":sc=sc+(100*m)
400 if rnd(0)>.4 then 570
410 print:print"you have found a chest."
420 print:print"who knows what it might contain. it"
430 print"could have gold and treasures, or"
440 print"perhaps a curse. it might even contain"
450 print"some food to boost your energy..."
460 print:print"do you want to open it";
470 c$="":input c$:if c$="y" then 490
480 if c$<>"n" then 470
490 f=0:ch=0:ch=int(rnd(0)*10+1):if ch=5 or ch=10 then f=1
500 if ch=2 or ch=7 then f=2
510 if ch=3 then f=3
520 if f=1 then print:?"you have found some food.":h=h+10:if h>100 then h=100
530 if f=2 then print:print"you found treasure!":sc=sc+500*int(rnd(0)*4+1)
540 if f=3 then print:print"you have disturbed a curse...aaaugh!"
550 if f=3 then h=h-int(rnd(0)*5+5):if h<=0 then 930
560 if c$="y" and f=0 then print:print"the chest is completely empty."
570 if rnd(0)>.1 then 690
580 print:print"you hear a voice. you look around to"
590 print"see who it is. you look down, to find a"
600 print"gnome. the gnome, who barely is the"
610 print"height of your leg up to the knee, says:"
620 print chr$(34);"i can assist you on your adventure. i"
630 print"can point out the correct tunnel through";
640 print"which you can continue finding the exit."
650 l=l+int(rnd(0)*2+2):if l>20 then l=20
660 print chr$(34);"may you have a safe journey.";chr$(34)
670 print:print"and with that he disappears into the"
680 print"darkness."
690 print:print"you have the choice of whether to"
700 print"proceed cautiously or to run."
710 print"(p or r)";
720 c$="":input c$:if c$="p" then 740
730 if c$<>"r" then 720
740 if c$="p" then print:print"you walk cautiously into the tunnel..."
750 if c$="r" then print:print"you dash through the tunnel..."
760 if c$="p" or (c$="r" and rnd(0)>.15) then 780
770 print"and cut yourself on the stalactites.":h=h-2:if h<=0 then 930
780 if c$="p" then l=l+1
790 if c$="r" then l=l+int(rnd(0)*2+2)
800 if l>=20 then 820
810 for tm=1 to 2000:next tm:goto 60
820 print:print"well done!"
830 print:print"you have exited cavern";cv;"safely."
840 print:print"you have a score of";sc:if cv=10 then 880
850 print:print"you get 200 points for successfully"
860 print"exiting the cavern. now onto cavern";cv+1;"."
870 cv=cv+1:sc=sc+200:l=1:for tm=1 to 5000:next tm:goto 60
880 for tm=1 to 5000:next tm:print"“"
890 print:print:print"you have made it out of the labyrinth"
900 print"of tunnels and caverns. you look around"
910 print"the open plain and quietly congratulate"
920 print"yourself.":goto 970
930 for tm=1 to 2000:next tm:print:print"everything turns black..."
940 for tm=1 to 4000:next tm
950 print"“":print:print:print:print:print:print:print:print:print:print:print
960 print"you have died."
970 print:print:print:for tm=1 to 4000:next tm
980 print"do you wish to play again";
990 c$="":input c$:if c$="y" then 40
1000 if c$<>"n" then 990
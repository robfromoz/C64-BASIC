0 rem caverns
1 rem by r vonk (c) 2000
2 rem
5 goto 1550
10 poke 53280,8:poke 53281,9
20 cv=1:h=100:sc=0:l=1
30 print"{clear}{yellow}{142}":print"you are in cavern";cv;"-";l
40 print:print"health:";h;"%"
50 print:print"your score is";sc
60 print:print:print
70 if rnd(0)>.7 then 200
80 t=int(rnd(0)*4+2)
81 d1=int(rnd(0)*t+1)
90 print"you see";t;"tunnels, all of them dark and"
100 print"possibly dangerous. which one do you"
110 print"want to take";
120 input c
130 if c<1 or c>t then 120
140 if c<>d1 then 170
150 print:print"you fall down a steep decline and injure";
160 print"yourself.":h=h-int(rnd(0)*3+2):l=l+1:goto 200
161 if h<=0 then 1400
170 print:print"you make your way through the tunnel"
180 print"safely."
190 l=l+1
200 if rnd(0)>.3 then 410
210 print:print"watch out!"
220 print
230 m=int(rnd(0)*4+1)
240 if m=1 then m$="gremlin"
250 if m=2 then m$="mutant bat"
260 if m=3 then m$="serpent"
270 if m=4 then m$="ghoul"
280 print:print"a ";m$;" guards this tunnel."
290 print:print"do you wish to fight or retreat"
300 input"(f or r)";c$
310 if c$="f" then 340
320 if c$<>"r" then 300
330 if rnd(0)<.2 then print:print"the ";m$;" stops you.":goto 340
331 print:print"you manage to escape.":goto 410
340 if rnd(0)>.4 then 390
350 print:print"*ugh* you've been defeated, and the"
360 print m$;" has left you with a severe"
370 print"wound.":h=h-int(rnd(0)*5+l)
380 if h<=0 then 1400
381 goto 410
390 print:print"you have slain the ";m$;"."
400 sc=sc+(100*m)
410 if rnd(0)>.4 then 590
420 print:print"you have found a chest."
430 print:print"who knows what it might contain. it"
440 print"could have gold and treasures, or"
450 print"perhaps a curse. it might even contain"
460 print"some food to boost your energy..."
470 print:print"do you want to open it";
480 c$="":input c$
490 if c$="y" then 510
500 if c$="n" then 580
501 goto 480
510 f=0:ch=0:ch=int(rnd(0)*10+1)
520 if ch=5 or ch=10 then f=1
530 if ch=2 or ch=7 then f=2
540 if ch=3 then f=3
550 if f=1 then print:print"you have found some food.":h=h+10:if h>100 then h=100
560 if f=2 then print:print"you found treasure!":sc=sc+500*int(rnd(0)*4+1)
570 if f=3 then print:print"you have disturbed a curse...aaaugh!"
571 if f=3 then h=h-int(rnd(0)*5+5)
572 if h<=0 then 1400
580 if c$="y" and f=0 then print:print"the chest is completely empty."
590 if rnd(0)>.1 then 700
600 print:print"you hear a voice. you look around to"
610 print"see who it is. you look down, to find a"
620 print"gnome. the gnome, who barely is the"
630 print"height of your leg up to the knee, says:"
640 print chr$(34);"i can assist you on your adventure. i"
650 print"can point out the correct tunnel through";
660 print"which you can continue finding the exit."
670 l=l+int(rnd(0)*2+2):if l>20 then l=20
680 print chr$(34);"may you have a safe journey.";chr$(34)
690 print:print"and with that he disappears into the"
691 print"darkness."
700 print:print"you have the choice of whether to"
710 print"proceed cautiously or to run."
711 print"(p or r)";
720 c$="":input c$
730 if c$="p" then 750
740 if c$<>"r" then 720
750 if c$="p" then print:print"you walk cautiously into the tunnel..."
760 if c$="r" then print:print"you dash through the tunnel..."
770 if c$="p" or (c$="r" and rnd(0)>.15) then 790
780 print"and cut yourself on the stalactites.":h=h-2:if h<=0 then 1400
790 if c$="p" then l=l+1
800 if c$="r" then l=l+int(rnd(0)*2+2)
810 if l>=20 then 830
820 for tm=1 to 2000:next tm:goto 30
830 print:print"well done!"
840 print:print"you have exited cavern";cv;"safely."
841 print:print"you have a score of";sc
850 if cv=10 then 900
860 print:print"you get 200 points for successfully"
870 print"exiting the cavern. now onto cavern";cv+1;"."
880 cv=cv+1:sc=sc+200:l=1
890 for tm=1 to 5000:next tm:goto 30
900 for tm=1 to 5000:next tm:print"“"
901 poke 53281,5:print:print:print"žyou have made it out of the labyrinth"
910 print"of tunnels and caverns. you look around"
920 print"the open plain and quietly congratulate"
930 print"yourself..."
940 for tm=1 to 4000:next tm
950 print:print:print"suddenly the sky turns as black as ink."
960 poke 53281,0
970 print"what was nice pleasant weather, typical"
980 print"of late spring, turned into a dry storm"
990 poke 53281,1
991 for tm=1 to 10:next tm
1000 poke 53281,0
1010 for tm=1 to 1000:next tm
1020 poke 53281,1
1021 for tm=1 to 10:next tm
1030 poke 53281,0
1040 for tm=1 to 500:next tm
1050 print:print"a face appears in the sky. you rub your"
1060 print"eyes and peer at it. it is kaeiri. he"
1070 print"says in a booming tone:"
1080 for tm=1 to 500:next tm
1090 print:print chr$(34);"so, my labyrinth was not as impossible"
1100 print"to escape from as i thought. i seem to"
1110 print"have underestimated you. i give my"
1120 print"congratulations...";chr$(34)
1130 print
1140 for tm=1 to 4000:next tm
1150 poke 53281,1
1160 for tm=1 to 10:next tm
1170 print"you are struck by a lightning bolt!"
1180 poke 53281,0
1190 print"you black out and fall to the ground..."
1200 for tm=1 to 4000:next tm
1210 print"{clear}{gray}":poke 53281,11
1220 for tm=1 to 2000:next tm
1230 print:print"you come to. it is now overcast, and"
1240 print"you still feel kaeiri's presence."
1250 for tm=1 to 2000:next tm
1260 print:print"you look up and find that there is a"
1270 print"huge, foreboding castle in the middle"
1280 print"of the plain."
1290 print:print
1300 print"kaeiri's voice booms: ";chr$(34);"the castle will"
1310 print"keep you occupied while i carry out my"
1320 print"plans to dominate the world!!!!!":for tm=1 to 1000:next tm:print
1330 print"{reverse on}ha ha ha ha ha ha ha ha ha ha ha ha!!{reverse off}";chr$(34)
1340 for tm=1 to 4000:next tm
1350 print:print"in an instant, you find yourself in a"
1360 print"small, dark room..."
1370 for tm=1 to 2000:next tm
1380 print:print:print"to be continued..."
1390 goto 1390
1400 for tm=1 to 2000:next tm
1410 poke 53281,0:print:print"everything turns black..."
1420 for tm=1 to 4000:next tm
1430 print"{clear}{red}":print:print:print:print:print:print:print:print:print:print:print
1440 print"you have died."
1450 print:print:print
1460 for tm=1 to 4000:next tm
1470 print"kaeiri will now accomplish his mission."
1480 print
1490 for tm=1 to 4000:next tm
1500 print"do you wish to play again";
1510 c$="":input c$
1520 if c$="y" then 10
1530 if c$="n" then end
1540 goto 1510
1550 print"{clear}{black}{142}":poke 53280,8:poke 53281,8
1560 print tab(10);"a r. vonk production"
1570 for tm=1 to 2000:next tm:print:print
1580 print tab(2);" ¦¦¦  ¦¦¦¦  ¦  ¦  ¦¦¦¦  ¦¦¦¦  ¦  ¦"
1590 print tab(2);"¦     ¦  ¦  ¦  ¦  ¦     ¦  ¦  ¦¦ ¦"
1600 print tab(2);"¦     ¦  ¦  ¦  ¦  ¦     ¦  ¦  ¦ ¦¦"
1610 print tab(2);"¦     ¦¦¦¦  ¦  ¦  ¦¦¦¦  ¦¦¦   ¦ ¦¦"
1620 print tab(2);"¦     ¦  ¦  ¦  ¦  ¦     ¦  ¦  ¦  ¦"
1630 print tab(2);"¦     ¦  ¦  ¦  ¦  ¦     ¦  ¦  ¦  ¦"
1640 print tab(2);" ¦¦¦  ¦  ¦   ¦¦   ¦¦¦¦  ¦  ¦  ¦  ¦"
1641 print:print tab(16);"(c) 2000"
1650 print:print:print
1660 for tm=1 to 4000:next tm
1670 print:print"do you want to read the story";
1680 input c$
1690 if c$="n" then 10
1700 if c$<>"y" then 1680
1710 poke 53281,9:print"{clear}{yellow}{014}"
1720 print chr$(34);"Don't be silly, Jimmy. You know there"
1730 print tab(5);"are no such things as ghosts."
1740 print:print"No matter what Mum and Dad said, and no"
1750 print"matter how much they tried to convince"
1760 print"you that you were imagining things, you"
1770 print"undeniably believed in ghosts, wizards"
1780 print"and trolls."
1790 print:print
1810 print"You, like any other rebellious teenager,";
1820 print"normally stay up late, watching TV."
1830 for tm=1 to 6000:next tm
1840 print:print"One night, you fell asleep while"
1850 print"watching a show called 'The World's Best";
1860 print"Magicians'. Before you nodded off, you"
1870 print"noted one, by the name of Leo Kaeiri."
1880 print"Kaeiri was, you thought, the 'coolest'"
1890 print"magician ever."
1900 for tm=1 to 6000:next tm
1910 print:print"You suddenly wake up...to find that you"
1920 print"left the TV on. The magicians' show was"
1930 print"just concluding."
1940 print:print
1950 for tm=1 to 4000:next tm
1960 print"As you reached for the ON/OFF button on"
1970 print"the TV, you felt this great surge of"
1980 print"electricity, and then, a face appears"
1990 print"on the screen. You recognise it"
2000 print"immediately--Kaieri."
2010 for tm=1 to 6000:next tm
2020 print:print"Kaeiri speaks: ";chr$(34);"YOU HAVE AROUSED THE"
2030 print"WRATH OF THE GREAT KAEIRI! NO MORTAL"
2040 print"BEING WOULD DARE TO DISTURB ME!"
2050 print:print"...and before you could say 'alakazam',"
2060 print"you found yourself in a dank cavern."
2070 for tm=1 to 6000:next tm
2080 print:print"Kaeiri appears out of nowhere."
2090 for tm=1 to 2000:next tm
2100 print:print chr$(34);"This is my cavern labyrinth. No-one has";
2110 print"escaped from it before. Which is why"
2120 print"you are here. You are the only mortal"
2130 print"being I know of who believes in the"
2140 print"in the paranormal, and therefore the"
2150 print"only mortal who can defeat me.";chr$(34)
2160 for tm=1 to 4000:next tm
2170 print:print"You ask: ";chr$(34);"What are you gonna do?";chr$(34)
2180 for tm=1 to 2000:next tm
2190 print:print chr$(34);"I intend to rule the world with my"
2200 print"immense power. So immense, that there"
2210 print"will be no-one who can stop me.";chr$(34)
2220 for tm=1 to 3000:next tm
2230 print:print"HA HA HA HA HA HA HA!!!!!!"
2240 for tm=1 to 2000:next tm
2250 print:print"And with that, he disappeared."
2260 for tm=1 to 3000:next tm
2270 print"{clear}"
2280 print:print"The cavern labyrinth contains 10 caverns";
2290 print"with 20 sections in each one. Throughout";
2300 print"the journey, you will come across many"
2310 print"things, such as monsters, treasure"
2320 print"chests, and gnomes."
2330 print:print"There are monsters galore in the"
2340 print"labyrinth. You can choose to fight"
2350 print"them, or retreat. Sometimes, when you"
2360 print"do try to run, the monsters will stop"
2370 print"you."
2380 print
2390 print"You may be lucky to find a treasure"
2400 print"chest in a section of the cavern, and"
2410 print"it may contain either a piece of food,"
2420 print"treasure or a curse. You are given a"
2430 print"choice of whether or not you want to"
2440 print"open a chest."
2450 get k$:if k$="" then 2450
2460 print"{clear}"
2470 print:print"Small gnomes inhabit the caves of the"
2480 print"labyrinth. Luckily for you, they are"
2490 print"very friendly and will help you on your"
2500 print"journey."
2510 print:print"Your health begins at 100%, and goes"
2520 print"down if you lose a fight, or if you"
2530 print"fall down a tunnel, or if you run and"
2540 print"cut yourself on a stalactite."
2550 print:print
2560 print"Good luck on your quest."
2570 get k$:if k$="" then 2570
2580 goto 10
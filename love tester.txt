10 rem love tester r vonk 20140805
20 poke 53280,2:poke 53281,1:poke 646,2
30 print"“":print"this program is called 'love tester'."
40 print:print"this is a computerised version of the"
50 print"little compatibility test you probably"
60 print"did (or probably do) at school during"
70 print"a class for fun."
80 print:print"here is how you do the test:"
90 print:print"1) write the names of both people you"
100 print"   wish to test."
110 print:print"2) count the number of occurrences of"
120 print"   the letters l, o, v, e and s in both"
130 print"   names, and write it down."
140 print:print"3) add each pair of consecutive digits"
150 print"   to get another number. if the number"
160 print"   has two digits, stop here."
170 print:print"4) otherwise, repeat step 3."
180 print:print"                              hit a key"
190 get i$:if i$="" then 190
200 print"“":print"the two-digit number is a percentage."
210 print:print"here is an example:"
220 print:print tab(14);"bill clinton"
230 print:print tab(17);"loves"
240 print:print tab(12);"monica lewinsky"
250 print:print tab(12);"4  2  0  1  1"
260 print:print tab(12);"  6  2  1  2"
270 print:print tab(12);"    8  3  3"
280 print:print tab(12);"     11  6"
290 print:print tab(12);"      2 7"
300 print:print"this couple is 27% compatible."
310 print:print"                              hit a key"
320 get i$:if i$="" then 320
330 print"“":print"if you find the compatibility number"
340 print"too low, try shortening or lengthening"
350 print"one or both of the names."
360 print:print"note: this is not a serious program"
370 print"to determine the compatibility of a"
380 print"couple, and should not be treated as"
390 print"such."
400 for i=1 to 14:print:next i:print"                              hit a key"
410 get i$:if i$="" then 410
420 l$="loves"
430 print"“":print"name of first person to test?":input m$
440 print:print"name of the second person?":input f$
450 print"“":print tab(20-len(m$)/2);m$
460 print:print tab(17);l$
470 print:print tab(20-len(f$)/2);f$
480 o$="":print:print tab(12);:n$=m$+f$:for i=1 to len(l$):o=0:for j=1 to len(n$)
490 if mid$(l$,i,1)=mid$(n$,j,1) then o=o+1
500 next j:print o;:if c>9 then o$=o$+right$(str$(o),2):goto 520
510 o$=o$+right$(str$(o),1)
520 next i
530 print:print:print tab(19-len(o$));:c$="":for i=1 to len(o$)-1
540 d=val(mid$(o$,i,1))+val(mid$(o$,i+1,1))
550 print d;:if d>9 then c$=c$+right$(str$(d),2):goto 570
560 c$=c$+right$(str$(d),1)
570 next i:if len(c$)=2 then 590
580 o$=c$:goto 530
590 print:print:print"this couple is ";c$;"% compatible."
600 print:print"another couple";
610 input i$:if i$<>"y" and i$<>"n" then print"(y) on (n) please.":goto 610
620 if i$="y" then 430
